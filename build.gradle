plugins {
    id 'com.android.application' version '7.3.0' apply false
    id 'com.android.library' version '7.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.18.1' apply false
    id 'com.github.ben-manes.versions' version '0.42.0' apply false
    id 'androidx.navigation.safeargs.kotlin' version '2.5.2' apply false
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        parallel = true
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'
    tasks.named("dependencyUpdates").configure {
        resolutionStrategy {
            componentSelection {
                all {
                    if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                        reject('Release candidate')
                    }
                }
            }
        }
    }
}